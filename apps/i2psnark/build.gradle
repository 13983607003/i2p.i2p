apply plugin: 'war'

sourceSets {
    main {
        java {
            srcDir 'java/src'
            include 'org/klomp/snark/web/**'
        }
    }
    jar {
        java {
            srcDir 'java/src'
            exclude 'org/klomp/snark/standalone'
            exclude 'org/klomp/snark/web'
        }
    }
}

dependencies {
    jarCompile project(':core')
    jarCompile project(':apps:ministreaming')
    jarCompile project(':apps:systray')
    providedCompile sourceSets.jar.output
    providedCompile project(':apps:ministreaming')
    providedCompile project(':apps:jetty')
}

task i2psnarkJar(type: Jar) {
    from sourceSets.jar.output
    manifest {
        attributes 'Main-Class': 'org.klomp.snark.Snark'
        attributes 'Class-Path': 'i2p.jar mstreaming.jar streaming.jar'
    }
}

artifacts {
    archives i2psnarkJar
}

war {
    into '.icons', {
        from 'icons'
    }
    webInf {
        into 'classes/org/klomp/snark/web'
        from 'mime.properties'
    }
    webXml = file('web.xml')
}
